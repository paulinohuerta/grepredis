#!/usr/bin/awk -f

BEGIN {
  a="/inet/tcp/0/localhost/6379"
  #db=SELECT(a,0)
  if(!db)
    print "Selecting default database..." 
  KEYS(a,"*")
  for (key in K) {
    TYPE(a,key)
  }
  for (i in K) {
    if(K[i]=="list"){
      cont=obtain(a,i,"LLEN") 
      if (cont > 0) {
        LRANGE(a,i,cont)
        for (j in L) 
          if(L[j] ~ ARGV[1])
	    printf("%s %s\n%s:%s\n","List ",i,j,L[j])
      }
    }
    if(K[i]=="hash"){
       HGETALL(a,i)
       for(j in H) {
         if(H[j] ~ ARGV[1])
           printf("%s %s\n%s:%s\n","Hash ",i,j,H[j])
       }
    }
  }
  close(a)
}

function SELECT(c,n){
  RS = ORS = "\r\n"
  print "SELECT",n |& c
  c |& getline
  db=substr($0,2)
  return db=="OK"
}

function LRANGE(c,k,r, cont,i,t,n){
  RS = ORS = "\r\n"
  print "LRANGE",k, "-"r, r |& c
  c |& getline
  cont=substr($0,2)
  cont=cont*2
  delete L
  n=1
  while (cont--){
    c |& getline
    if((cont%2)==0) {
      T=$0
      while((length(T)) < (t+0)) {
        T=T"\r\n"
        c |& getline
        T=T""$0
      }
      L[n++]=T
      T=""
    }
    else {
      t=substr($0,2)
    }
  }
}

function TYPE(c,key){
  RS = ORS = "\r\n"
  print "TYPE",key |& c 
  c |& getline
  K[key]=substr($0,2)
}

function HGETALL(c,key, cont) {
  cont=obtain(c,key,"HGETALL")
  cont=cont*2
  hash(c,cont)
}

function obtain(c,key,cdo, cont){
  RS = ORS = "\r\n"
  print cdo,key  |& c
  c |& getline
  cont=$0
  cont=substr($0,2)
  return cont
}

function KEYS(c,k, cont){
  cont=obtain(c,k,"KEYS")*2
  while (cont--){
    c |& getline
    if((cont%2)==0) 
      K[$0]
  }
}

function hash(c,cont, prev,T,t){
  delete H
  while (cont--){
   c |& getline
   if((cont%2)==0) {
      T=$0
      while((length(T)) < (t+0)) {
        T=T"\r\n"
        c |& getline
        T=T""$0
      }
      if(!prev)
       prev=T
      else {
       H[prev]=T
       prev=0
      }
      T=""
   }
   else {
     t=substr($0,2)
   }
  }
}
