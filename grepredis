#!/usr/bin/awk -f

BEGIN {
  a="/inet/tcp/0/localhost/6379"
# db=SELECT(a,1)
  KEYS(a,"*")
  TYPE(a,K)
  for (i in K) {
    if(K[i]=="hash"){
      HGETALL(a,i)
      for(j in H) {
        if(H[j]==ARGV[1] || H[j] ~ ARGV[1]){
          print i
          print j":"H[j]

        }
      }
    }
  }
  for (i in K) {
    if(K[i]=="list"){
      cont=obtain(a,i,"LLEN") 
      if (cont > 0) {
      LRANGE(a,i,cont)
      for (j in L) 
         if(L[j]==ARGV[1] || L[j] ~ ARGV[1]){
           print "** "i" **"
           print "   ** "j" **"
           print L[j]
         }
      }
    }
  }
  close(a)
}
function SELECT(c,n){
  RS = ORS = "\r\n"
  print "SELECT",n |& c
  c |& getline
  db=substr($0,2)
  return db=="OK"
}
function LRANGE(c,k,r, cont,i,t){
  RS = ORS = "\r\n"
  print "LRANGE",k, "-"r, r |& c
  c |& getline
  cont=substr($0,2)
  cont=cont*2
  delete L
  n=1
  while (cont--){
    c |& getline
    if((cont%2)==0) {
      if(length($0)==(t+0)) {
        L[n++]=$0
      }
      else{
        T=T""$0"\r\n"
        while((length(T)) < (t+0)) {
         c |& getline
         T=T""$0"\r\n"
        }
        L[n++]=T
        T=""
      }
    }
    else {
     t=substr($0,2)
    }
  }
}
function TYPE(c,K, i){
  RS = ORS = "\r\n"
  for (i in K){
    print "TYPE",i |& c
    c |& getline
    K[i]=substr($0,2)
  }
}
function HGETALL(c,key, cont) {
   cont=obtain(c,key,"HGETALL")
   cont=cont*2
   hash(c,cont)
}
function obtain(c,key,cdo, cont){
  RS = ORS = "\r\n"
  print cdo,key  |& c
  c |& getline
  cont=$0
  cont=substr($0,2)
  return cont
}
function hash(c,cont, i,prev){
  delete H
  while (cont--){
   c |& getline
   if((cont%2)==0) {
     if(!prev)
       prev=$0
     else {
       H[prev]=$0
       prev=0
     }
   }
  }
}
function KEYS(c,k){
  cont=obtain(c,k,"KEYS")*2
  while (cont--){
    c |& getline
    if((cont%2)==0) {
     K[$0]
    }
  }
}
